
#Область ПроверкаОбъектовМетаданных

Функция ПрефиксыОбъектовНеНаПоддержке() Экспорт
	возврат мт_ТестированиеКонфигурации_Настройки.ПрефиксыОбъектовНеНаОсновнойПоддержке();
КонецФункции

Процедура ПроверитьНаличиеДвижений(МетаданныеДокумента, ТребуемыеДвижения, Отказ, ПроверенныеДвижения) Экспорт
	
	мСтрокиОшибок = Новый Массив;
	мСтрокиОшибок.Добавить("В документе "+МетаданныеДокумента.Имя + " не указаны движения по регистрам:");
	
	Для Каждого МетаданныеРегистра Из ТребуемыеДвижения Цикл
		
		Если НЕ МетаданныеДокумента.Движения.Содержит(МетаданныеРегистра) Тогда
			мСтрокиОшибок.Добавить(МетаданныеРегистра.ПолноеИмя());
		КонецЕсли;
		
	КонецЦикла;
		
	Если мСтрокиОшибок.Количество()>1 Тогда
		СообщитьОбОшибке(мСтрокиОшибок, Отказ);
	КонецЕсли;
	
	ПроверенныеДвижения.Вставить(МетаданныеДокумента, ТребуемыеДвижения);
	
КонецПроцедуры

Процедура ПроверитьВхождениеТипов_в_ТипМетаданного(ОбъектМетаданных, ТребуемыеТипы, Отказ, ПроверенныеТипы) Экспорт
	
	ЗаголовокСообщенияОбОшибке = "В типах объекта "+ОбъектМетаданных.ПолноеИмя() + " не указан тип:";
	
	мСтрокиОшибок = Новый Массив;
	мСтрокиОшибок.Добавить(ЗаголовокСообщенияОбОшибке);
	
	Для Каждого Тип Из ТребуемыеТипы Цикл
		
		Если НЕ ОбъектМетаданных.Тип.СодержитТип(Тип) Тогда
			мСтрокиОшибок.Добавить(Строка(Тип));
		КонецЕсли;
		
	КонецЦикла;
		
	Если мСтрокиОшибок.Количество()>1 Тогда
		СообщитьОбОшибке(мСтрокиОшибок, Отказ);
	КонецЕсли;
	
	ПроверенныеТипы.Вставить(ОбъектМетаданных, Новый ОписаниеТипов(ТребуемыеТипы));
	
КонецПроцедуры

Процедура ПроверитьВхождениеТипов_в_ТипПараметраКоманды(ОбъектМетаданных, ТребуемыеТипы, Отказ, ПроверенныеТипы) Экспорт
	
	ЗаголовокСообщенияОбОшибке = "В типах объекта "+ОбъектМетаданных.ПолноеИмя() + " не указан тип:";
	
	мСтрокиОшибок = Новый Массив;
	мСтрокиОшибок.Добавить(ЗаголовокСообщенияОбОшибке);
	
	Для Каждого Тип Из ТребуемыеТипы Цикл
		
		Если НЕ ОбъектМетаданных.ТипПараметраКоманды.СодержитТип(Тип) Тогда
			мСтрокиОшибок.Добавить(Строка(Тип));
		КонецЕсли;
		
	КонецЦикла;
		
	Если мСтрокиОшибок.Количество()>1 Тогда
		СообщитьОбОшибке(мСтрокиОшибок, Отказ);
	КонецЕсли;
	
	ПроверенныеТипы.Вставить(ОбъектМетаданных, Новый ОписаниеТипов(ТребуемыеТипы));
	
КонецПроцедуры

Процедура ПроверитьВхождениеВСостав(ОбъектМетаданных, ТребуемыйСостав, Отказ, ПроверенныйСостав) Экспорт
	
	ЗаголовокСообщенияОбОшибке = "В составе критерия отбора " + ОбъектМетаданных.ПолноеИмя() + " не указаны:";
	ПроверитьНаличиеМетаданныхВКоллекции(ОбъектМетаданных.Состав, ТребуемыйСостав, Отказ, ЗаголовокСообщенияОбОшибке);
	
	ПроверенныйСостав.Вставить(ОбъектМетаданных, ТребуемыйСостав);
	
КонецПроцедуры

Процедура ПроверитьВхождение_в_ВводНаОсновании(ОбъектМетаданных, ТребуемыеЭлементы, Отказ, ПроверенныйВводНаОсновании) Экспорт
	
	ЗаголовокСообщенияОбОшибке = "В перечне ""вводится на основании"" " + ОбъектМетаданных.ПолноеИмя() + " не указаны:";
	ПроверитьНаличиеМетаданныхВКоллекции(ОбъектМетаданных.ВводитсяНаОсновании, ТребуемыеЭлементы, Отказ, ЗаголовокСообщенияОбОшибке);
	
	ПроверенныйВводНаОсновании.Вставить(ОбъектМетаданных, ТребуемыеЭлементы);
	
КонецПроцедуры

Процедура ПроверитьНаличиеМетаданныхВКоллекции(КоллекцияОбъектовМетаданных, ТребуемыеЭлементы, Отказ, ЗаголовокСообщенияОбОшибке) Экспорт
	
	мСтрокиОшибок = Новый Массив;
	мСтрокиОшибок.Добавить(ЗаголовокСообщенияОбОшибке);
	
	Для Каждого ОбъектМетаданных Из ТребуемыеЭлементы Цикл
		
		Если НЕ КоллекцияОбъектовМетаданных.Содержит(ОбъектМетаданных) Тогда
			мСтрокиОшибок.Добавить(ОбъектМетаданных.ПолноеИмя());
		КонецЕсли;
		
	КонецЦикла;
		
	Если мСтрокиОшибок.Количество()>1 Тогда
		СообщитьОбОшибке(мСтрокиОшибок, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьНепроверенныеДвиженияПоДокументам(ПроверенныеДвижения, Отказ) Экспорт
	
	мСтрокиОшибок = Новый Массив;
	мСтрокиОшибок.Добавить("Не выполняется проверка движений по регистрам:");
	
	РегистрыНеНаПоддержке = Новый Массив;
	
	Для Каждого ОбъектМетаданных Из Метаданные.РегистрыНакопления Цикл
		Если Не ОбъектНаПоддержке(ОбъектМетаданных) Тогда
			РегистрыНеНаПоддержке.Добавить(ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбъектМетаданных Из Метаданные.РегистрыСведений Цикл
		Если Не ОбъектНаПоддержке(ОбъектМетаданных) Тогда
			РегистрыНеНаПоддержке.Добавить(ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого МетаданныеДокумента Из Метаданные.Документы Цикл
		
		Если ОбъектНаПоддержке(МетаданныеДокумента) Тогда
			
			Для Каждого МетаданныеРегистра Из РегистрыНеНаПоддержке Цикл
				
				Если МетаданныеДокумента.Движения.Содержит(МетаданныеРегистра)
					И Не ЕстьВПроверенныхДвижениях(ПроверенныеДвижения, МетаданныеДокумента, МетаданныеРегистра) Тогда
					мСтрокиОшибок.Добавить(СтрШаблон("%1 %2", МетаданныеДокумента.Имя, МетаданныеРегистра.ПолноеИмя()))
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если мСтрокиОшибок.Количество()>1 Тогда
		СообщитьОбОшибке(мСтрокиОшибок, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура СообщитьОНепроверенныхТипах(ПроверенныеТипы, Отказ, ГруппаОбъектовМетаданных, ЗаголовокСообщенияОбОшибке) Экспорт
	
	НепроверенныеТипы = НепроверенныеТипы(ГруппаОбъектовМетаданных, ПроверенныеТипы);
	
	Если НепроверенныеТипы.Количество() > 0 Тогда
	
		мСтрокиОшибок = Новый Массив;
		мСтрокиОшибок.Добавить(ЗаголовокСообщенияОбОшибке);
	
		Для Каждого КлючИЗначение Из НепроверенныеТипы Цикл
			ОбъектаМетаданных = КлючИЗначение.Ключ;
			мНепроверенныеТипы = КлючИЗначение.Значение;
			Для Каждого НепроверенныйТип Из мНепроверенныеТипы Цикл
				мСтрокиОшибок.Добавить(СтрШаблон("%1 %2", ОбъектаМетаданных.Имя, Строка(НепроверенныйТип)))
			КонецЦикла;
		КонецЦикла;
		
		СообщитьОбОшибке(мСтрокиОшибок, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Функция НепроверенныеТипы(ГруппаОбъектовМетаданных, ПроверенныеТипы = Неопределено)
	
	Результат = Новый Соответствие;
	
	ТипыОбъектовНеНаПоддержке = Новый Массив;
	
	Для Каждого ОбъектМетаданных Из Метаданные.Справочники Цикл
		Если Не ОбъектНаПоддержке(ОбъектМетаданных) Тогда
			ТипыОбъектовНеНаПоддержке.Добавить(Тип("СправочникСсылка."+ОбъектМетаданных.Имя));
			ТипыОбъектовНеНаПоддержке.Добавить(Тип("СправочникОбъект."+ОбъектМетаданных.Имя));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбъектМетаданных Из Метаданные.Документы Цикл
		Если Не ОбъектНаПоддержке(ОбъектМетаданных) Тогда
			ТипыОбъектовНеНаПоддержке.Добавить(Тип("ДокументСсылка."+ОбъектМетаданных.Имя));
			ТипыОбъектовНеНаПоддержке.Добавить(Тип("ДокументОбъект."+ОбъектМетаданных.Имя));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбъектМетаданных Из Метаданные.БизнесПроцессы Цикл
		Если Не ОбъектНаПоддержке(ОбъектМетаданных) Тогда
			ТипыОбъектовНеНаПоддержке.Добавить(Тип("БизнесПроцессСсылка."+ОбъектМетаданных.Имя));
			ТипыОбъектовНеНаПоддержке.Добавить(Тип("БизнесПроцессОбъект."+ОбъектМетаданных.Имя));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбъектМетаданных Из ГруппаОбъектовМетаданных Цикл
		
		Если ОбъектНаПоддержке(ОбъектМетаданных) Тогда
			
			мНепроверенныеТипы = Новый Массив;
			
			Для Каждого Тип Из ТипыОбъектовНеНаПоддержке Цикл
				
				Если ОбъектМетаданных.Тип.СодержитТип(Тип)
					И Не ЕстьВПроверенныхТипах(ПроверенныеТипы, ОбъектМетаданных, Тип) Тогда
					
					мНепроверенныеТипы.Добавить(Тип);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если мНепроверенныеТипы.Количество() > 0 Тогда
				Результат.Вставить(ОбъектМетаданных, мНепроверенныеТипы);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура СообщитьОНепроверенномСоставе(ПроверенныйСостав, Отказ, ГруппаОбъектовМетаданных, ЗаголовокСообщенияОбОшибке) Экспорт
	
	НепроверенныйСостав = НепроверенныйСостав(ГруппаОбъектовМетаданных, ПроверенныйСостав);
	
	Если НепроверенныйСостав.Количество() > 0 Тогда
	
		мСтрокиОшибок = Новый Массив;
		мСтрокиОшибок.Добавить(ЗаголовокСообщенияОбОшибке);
	
		Для Каждого КлючИЗначение Из НепроверенныйСостав Цикл
			ОбъектМетаданных = КлючИЗначение.Ключ;
			мНепроверенныйСостав = КлючИЗначение.Значение;
			Для Каждого МетаданныеЭлемента Из мНепроверенныйСостав Цикл
				мСтрокиОшибок.Добавить(СтрШаблон("%1 %2", ОбъектМетаданных.Имя, МетаданныеЭлемента.ПолноеИмя()))
			КонецЦикла;
		КонецЦикла;
	
		СообщитьОбОшибке(мСтрокиОшибок, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Функция НепроверенныйСостав(ГруппаОбъектовМетаданных, ПроверенныйСостав = Неопределено)
	
	Результат = Новый Соответствие;
	
	Для Каждого ОбъектМетаданных Из ГруппаОбъектовМетаданных Цикл
		
		Если ОбъектНаПоддержке(ОбъектМетаданных) Тогда
			
			мНепроверенныйСостав = Новый Массив;
			
			Для Каждого МетаданныеЭлемента Из ОбъектМетаданных.Состав Цикл
				
				Если Не ОбъектНаПоддержке_ВключаяВложенные(МетаданныеЭлемента)
					И Не ЕстьВПроверенном(ПроверенныйСостав, ОбъектМетаданных, МетаданныеЭлемента) Тогда
					
					мНепроверенныйСостав.Добавить(МетаданныеЭлемента);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если мНепроверенныйСостав.Количество() > 0 Тогда
				Результат.Вставить(ОбъектМетаданных, мНепроверенныйСостав);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ПолучитьНепроверенныйВводНаОсновании(ПроверенныйВводНаОсновании, Отказ, ГруппаОбъектовМетаданных, ЗаголовокСообщенияОбОшибке) Экспорт
	
	мСтрокиОшибок = Новый Массив;
	мСтрокиОшибок.Добавить(ЗаголовокСообщенияОбОшибке);
	
	Для Каждого ОбъектМетаданных Из ГруппаОбъектовМетаданных Цикл
		
		Если ОбъектНаПоддержке(ОбъектМетаданных) Тогда
			
			Для Каждого МетаданныеЭлемента Из ОбъектМетаданных.ВводитсяНаОсновании Цикл
				
				Если Не ОбъектНаПоддержке_ВключаяВложенные(МетаданныеЭлемента)
					И Не ЕстьВПроверенном(ПроверенныйВводНаОсновании, ОбъектМетаданных, МетаданныеЭлемента) Тогда
					мСтрокиОшибок.Добавить(СтрШаблон("%1 %2", ОбъектМетаданных.Имя, МетаданныеЭлемента.ПолноеИмя()))
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если мСтрокиОшибок.Количество()>1 Тогда
		СообщитьОбОшибке(мСтрокиОшибок, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьВПроверенныхДвижениях(ПроверенныеДвижения, МетаданныеДокумента, МетаданныеРегистра) Экспорт
	
	ПроверенныеДвиженияПоДокументу = ПроверенныеДвижения[МетаданныеДокумента];
	Если ПроверенныеДвиженияПоДокументу = Неопределено Тогда
		возврат Ложь;
	КонецЕсли;
	
	возврат ПроверенныеДвиженияПоДокументу.Найти(МетаданныеРегистра)<>Неопределено;
	
КонецФункции

Функция ЕстьВПроверенныхТипах(ПроверенныеТипы, ОбъектМетаданных, Тип) Экспорт
	
	Если ПроверенныеТипы = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОписаниеТипов = ПроверенныеТипы[ОбъектМетаданных];
	Если ОписаниеТипов = Неопределено Тогда
		возврат Ложь;
	КонецЕсли;
	
	возврат ОписаниеТипов.СодержитТип(Тип);
	
КонецФункции

Функция ЕстьВПроверенном(Проверенные, ОбъектМетаданных, МетаданныеЭлемента)
	
	Если Проверенные = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПроверенныеОбъекта = Проверенные[ОбъектМетаданных];
	Если ПроверенныеОбъекта = Неопределено Тогда
		возврат Ложь;
	КонецЕсли;
	
	возврат ПроверенныеОбъекта.Найти(МетаданныеЭлемента)<>Неопределено;
	
КонецФункции

Процедура СообщитьОбОшибке(СтрокиОшибок, Отказ) Экспорт
	
	Отказ = Истина;
	Если ТипЗнч(СтрокиОшибок) = Тип("Строка") Тогда 
		ТекстСообщенияОбОшибке = СтрокиОшибок;
	Иначе
		//массив
		ТекстСообщенияОбОшибке = СтрСоединить(СтрокиОшибок, Символы.ПС + Символы.ВК);
	КонецЕсли;
	ЗаписьЖурналаРегистрации("Проверка конфигурации Test", УровеньЖурналаРегистрации.Ошибка,,,ТекстСообщенияОбОшибке);
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщенияОбОшибке);
		
КонецПроцедуры

Функция ОбъектНаПоддержке(ОбъектМетаданных, ПроверкаСоставного=Ложь) Экспорт
	
	//Невозможно определить программно, находится объект на поддержке или нет (8.3.17) 
	//Поэтому определяем по префиксу имени
	
	ПрефиксыОбъектовНеНаПоддержке = ПрефиксыОбъектовНеНаПоддержке();
	
	//ищем в начале
	Для Каждого Префикс Из ПрефиксыОбъектовНеНаПоддержке Цикл
		Если СтрНачинаетсяС(ОбъектМетаданных.Имя, Префикс) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если ПроверкаСоставного Тогда
		//ищем в середине, определяет объекты типа Метаданные.Документы.ЭтапПроизводства2_2.ТабличныеЧасти.мт_Сборки.Реквизиты.ДокументСборки
		Для Каждого Префикс Из ПрефиксыОбъектовНеНаПоддержке Цикл
			ПодстрокаПоиска = "."+Префикс;
			Если СтрНайти(ОбъектМетаданных.ПолноеИмя(), ПодстрокаПоиска)<>0 Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ОбъектНаПоддержке_ВключаяВложенные(ОбъектМетаданных) Экспорт
	
	возврат ОбъектНаПоддержке(ОбъектМетаданных, Истина);
	
КонецФункции

Функция ПроверитьСоответствиеПравНаПросмотр(ПроверяемыеМетаданные, Отказ) Экспорт
	
	ПроверяемоеПраво = "Просмотр";
	
	Для Каждого КлючИЗначение Из ПроверяемыеМетаданные Цикл
		
		МетаданныеОбъекта = КлючИЗначение.Ключ;
		МетаданныеПрисоединенногоФайла = КлючИЗначение.Значение;
		
		мСтрокиОшибок = Новый Массив;
		мСтрокиОшибок.Добавить(
			СтрШаблон("Различаются права на просмотр объектов %1 и %2",
				МетаданныеОбъекта.ПолноеИмя(),
				МетаданныеПрисоединенногоФайла.ПолноеИмя()
			)
		);
		
		Для Каждого Роль Из Метаданные.Роли Цикл
		
			ИмеетсяПравоНаОбъект = ПравоДоступа(ПроверяемоеПраво, МетаданныеОбъекта, Роль);
			ИмеетсяПравоНаПрисоединенныйФайл = ПравоДоступа(ПроверяемоеПраво, МетаданныеПрисоединенногоФайла, Роль);
			
			Если ИмеетсяПравоНаОбъект<>ИмеетсяПравоНаПрисоединенныйФайл Тогда
				мСтрокиОшибок.Добавить(Роль.ПолноеИмя());
			КонецЕсли;
			
		КонецЦикла;
		
		Если мСтрокиОшибок.Количество()>1 Тогда
			СообщитьОбОшибке(мСтрокиОшибок, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

#КонецОбласти

#Область СинтаксическаяПроверкаСхемКомпоновкиДанныхОтчетов

Процедура СинтаксическаяПроверкаСхемКомпоновкиДанныхДобавленныхОтчетов(Отказ) Экспорт
	
	//Идеи взяты отсюда: https://github.com/vanessa-opensource/add/tree/master/tests/smoke/тесты_ПроверкаМакетовСКД
	//Сделал проверку только отчетов. Не сделал проверку вложенных схем
	//При случае доделать

	Для Каждого ОбъектМетаданных Из Метаданные.Отчеты Цикл
		
		Менеджер = Отчеты;
		
		Если Не мт_ТестированиеКонфигурации_Служебный.ОбъектНаПоддержке(ОбъектМетаданных) Тогда
			Для Каждого МетаданныеМакета Из ОбъектМетаданных.Макеты Цикл
				
				Если МетаданныеМакета.ТипМакета = Метаданные.СвойстваОбъектов.ТипМакета.СхемаКомпоновкиДанных Тогда
					
					СхемаКомпоновкиДанных = Менеджер[ОбъектМетаданных.Имя].ПолучитьМакет(МетаданныеМакета.Имя);
					ПроверитьСхемуСКД(СхемаКомпоновкиДанных, Отказ, ОбъектМетаданных.ПолноеИмя());
					
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСхемуСКД(СхемаКомпоновкиДанных, Отказ, ПолноеИмяОбъектаМетаданных) Экспорт

	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных;
	
	Попытка
		ИсточникДоступныхНастроекКомпоновкиДанных = Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных);
		// Тут проходит синтаксический анализ запроса.
    	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(ИсточникДоступныхНастроекКомпоновкиДанных);
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = "Ошибка при проверке схемы компоновки данных " + ПолноеИмяОбъектаМетаданных 
			+ Символы.ВК
			+ ТекстСообщения;
		СообщитьОбОшибке(ТекстСообщения, Отказ);
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти


#Область АвтоматическаяГенерацияТекстовМодуля_ТестированиеКонфигурации_Сервер

// Сгенерировать текст области проверка движений.
// 
// Возвращаемое значение:
//  ТекстовыйДокумент - Сгенерировать текст области проверка движений
Функция СгенерироватьТекстОбласти_ПроверкаДвижений() Экспорт
	
	РегистрыНеНаПоддержке = Новый Массив;
	
	Для Каждого ОбъектМетаданных Из Метаданные.РегистрыНакопления Цикл
		Если Не ОбъектНаПоддержке(ОбъектМетаданных) Тогда
			РегистрыНеНаПоддержке.Добавить(ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбъектМетаданных Из Метаданные.РегистрыСведений Цикл
		Если Не ОбъектНаПоддержке(ОбъектМетаданных) Тогда
			РегистрыНеНаПоддержке.Добавить(ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	ДокументыКПроверке = Новый Структура;
	
	Для Каждого МетаданныеДокумента Из Метаданные.Документы Цикл
		
		Если ОбъектНаПоддержке(МетаданныеДокумента) Тогда
			
			мРегистрыКПроверке = Новый Массив;
		
			Для Каждого МетаданныеРегистра Из РегистрыНеНаПоддержке Цикл
				
				Если МетаданныеДокумента.Движения.Содержит(МетаданныеРегистра) Тогда
					Если ЭтоРегистрНакопления(МетаданныеРегистра) Тогда
						мРегистрыКПроверке.Добавить("Метаданные.РегистрыНакопления."+МетаданныеРегистра.Имя);
					Иначе
						мРегистрыКПроверке.Добавить("Метаданные.РегистрыСведений."+МетаданныеРегистра.Имя);
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
			Если мРегистрыКПроверке.Количество() > 0 Тогда
				ДокументыКПроверке.Вставить(МетаданныеДокумента.Имя, мРегистрыКПроверке);
			КонецЕсли;

		КонецЕсли;
		
	КонецЦикла;
	
	мСтр = Новый Массив;
	
	мСтр.Добавить("#Область ПроверкаДвижений");
	мСтр.Добавить();
	
	мСтр.Добавить("Процедура ПроверитьДвижения(Отказ)");
	мСтр.Добавить(Символы.Таб);
	мСтр.Добавить(Символы.Таб + "ПроверенныеДвижения = Новый Соответствие;");
	мСтр.Добавить(Символы.Таб);
	
	Для Каждого ЭлементДокумент Из ДокументыКПроверке Цикл
		мСтр.Добавить(Символы.Таб + "ПроверитьДвижения_" + ЭлементДокумент.Ключ + "(Отказ, ПроверенныеДвижения);");
	КонецЦикла;
	
	мСтр.Добавить(Символы.Таб);
	мСтр.Добавить(Символы.Таб + "мт_ТестированиеКонфигурации_Служебный.ПолучитьНепроверенныеДвиженияПоДокументам(ПроверенныеДвижения, Отказ);");
	мСтр.Добавить(Символы.Таб);
	мСтр.Добавить("КонецПроцедуры");
	мСтр.Добавить();
	
	Для Каждого ЭлементДокумент Из ДокументыКПроверке Цикл
		мСтр.Добавить("Процедура ПроверитьДвижения_" + ЭлементДокумент.Ключ + "(Отказ, ПроверенныеДвижения)");
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "МетаданныеДокумента = Метаданные.Документы." + ЭлементДокумент.Ключ + ";");
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "ТребуемыеДвижения = Новый Массив;");
		Для Каждого ЭлементРегистр Из ЭлементДокумент.Значение Цикл
			мСтр.Добавить(Символы.Таб + "ТребуемыеДвижения.Добавить(" + ЭлементРегистр + ");");
		КонецЦикла;
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "мт_ТестированиеКонфигурации_Служебный.ПроверитьНаличиеДвижений(МетаданныеДокумента, ТребуемыеДвижения, Отказ, ПроверенныеДвижения);");
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить("КонецПроцедуры");
		мСтр.Добавить();
	КонецЦикла;
	
	мСтр.Добавить("#КонецОбласти");
	мСтр.Добавить();
	
	ТекстОбласти = Новый ТекстовыйДокумент;
	Для Каждого Строчка Из мСтр Цикл
		ТекстОбласти.ДобавитьСтроку(Строчка);
	КонецЦикла;
	
	Возврат ТекстОбласти;
	
КонецФункции
	
// Сгенерировать текст области проверка критериев отбора.
// 
// Возвращаемое значение:
//  ТекстовыйДокумент - Сгенерировать текст области проверка критериев отбора
Функция СгенерироватьТекстОбласти_ПроверкаКритериевОтбора() Экспорт
	
	ГруппаОбъектомМетаданных = Метаданные.КритерииОтбора;
	
	НепроверенныеТипы = НепроверенныеТипы(ГруппаОбъектомМетаданных);
	НепроверенныйСостав = НепроверенныйСостав(ГруппаОбъектомМетаданных);
	
	ОбъединенныеНепроверенные = Новый Соответствие;
	Для Каждого КлючИЗначение Из НепроверенныеТипы Цикл
		ОбъединенныеНепроверенные.Вставить(КлючИЗначение.Ключ);
	КонецЦикла;
	Для Каждого КлючИЗначение Из НепроверенныйСостав Цикл
		ОбъединенныеНепроверенные.Вставить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	мСтр = Новый Массив;
	
	мСтр.Добавить("#Область ПроверкаКритериевОтбора");
	мСтр.Добавить();
	
	мСтр.Добавить("Процедура ПроверитьКритерииОтбора(Отказ) Экспорт");
	мСтр.Добавить(Символы.Таб);
	мСтр.Добавить(Символы.Таб + "ПроверенныеТипы = Новый Соответствие;");
	мСтр.Добавить(Символы.Таб + "ПроверенныйСостав = Новый Соответствие;");
	мСтр.Добавить(Символы.Таб);
	
	Для Каждого КлючИЗначение Из ОбъединенныеНепроверенные Цикл
		ОбъектМетаданных = КлючИЗначение.Ключ;
		мСтр.Добавить(Символы.Таб + "ПроверитьКритерийОтбора_" + ОбъектМетаданных.Имя + "(Отказ, ПроверенныеТипы, ПроверенныйСостав);");
	КонецЦикла;
	
	мСтр.Добавить(Символы.Таб);
	мСтр.Добавить(Символы.Таб + "ЗаголовокСообщенияОбОшибке = ""Не выполняется проверка типов критериев отбора:"";");
	мСтр.Добавить(Символы.Таб + "мт_ТестированиеКонфигурации_Служебный.СообщитьОНепроверенныхТипах(ПроверенныеТипы, Отказ, Метаданные.КритерииОтбора, ЗаголовокСообщенияОбОшибке);");
	мСтр.Добавить(Символы.Таб);
	мСтр.Добавить(Символы.Таб + "ЗаголовокСообщенияОбОшибке = ""Не выполняется проверка состава критериев отбора:"";");
	мСтр.Добавить(Символы.Таб + "мт_ТестированиеКонфигурации_Служебный.СообщитьОНепроверенномСоставе(ПроверенныйСостав, Отказ, Метаданные.КритерииОтбора, ЗаголовокСообщенияОбОшибке);");
	мСтр.Добавить(Символы.Таб);
	мСтр.Добавить("КонецПроцедуры");
	мСтр.Добавить();
	
	Для Каждого КлючИЗначение Из ОбъединенныеНепроверенные Цикл
		
		ОбъектМетаданных = КлючИЗначение.Ключ;
		
		мСтр.Добавить("Процедура ПроверитьКритерийОтбора_" + ОбъектМетаданных.Имя + "(Отказ, ПроверенныеТипы, ПроверенныйСостав)");
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "ОбъектМетаданных = Метаданные.КритерииОтбора." + ОбъектМетаданных.Имя + ";");
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "ОписаниеТипов = ОбъектМетаданных.Тип;");
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "ТребуемыеТипы = Новый Массив;");
		
		мТребуемыеТипы = НепроверенныеТипы[ОбъектМетаданных];
		Если мТребуемыеТипы<>Неопределено Тогда
			Для Каждого Тип Из мТребуемыеТипы Цикл
				СтроковоеПредставлениеТипа = СтроковоеПредставлениеТипа(Тип);
				мСтр.Добавить(Символы.Таб + "ТребуемыеТипы.Добавить(Тип(""" + СтроковоеПредставлениеТипа + """));");
			КонецЦикла;
		КонецЕсли;
		
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "мт_ТестированиеКонфигурации_Служебный.ПроверитьВхождениеТипов_в_ТипМетаданного(ОбъектМетаданных, ТребуемыеТипы, Отказ, ПроверенныеТипы);");
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "ТребуемыйСостав = Новый Массив;");
		
		мТребуемыйСостав = НепроверенныйСостав[ОбъектМетаданных];
		Если мТребуемыйСостав <> Неопределено Тогда
			Для Каждого ЭлементСостава Из мТребуемыйСостав Цикл
				ПредставлениеМетаданного = ПреобразоватьПолноеИмяМетаданногоКоМножественномуЧислу(ЭлементСостава.ПолноеИмя());
				мСтр.Добавить(Символы.Таб + "ТребуемыйСостав.Добавить(Метаданные." + ПредставлениеМетаданного + ");");
			КонецЦикла;
		КонецЕсли;
	
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "мт_ТестированиеКонфигурации_Служебный.ПроверитьВхождениеВСостав(ОбъектМетаданных, ТребуемыйСостав, Отказ, ПроверенныйСостав);");
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить("КонецПроцедуры");
		мСтр.Добавить();
		
	КонецЦикла;
	
	мСтр.Добавить("#КонецОбласти");
	мСтр.Добавить();
	
	ТекстОбласти = Новый ТекстовыйДокумент;
	Для Каждого Строчка Из мСтр Цикл
		ТекстОбласти.ДобавитьСтроку(Строчка);
	КонецЦикла;
	
	Возврат ТекстОбласти;
	
КонецФункции
	
// Сгенерировать текст области ОпределяемыеТипы.
// 
// Возвращаемое значение:
//  ТекстовыйДокумент - Сгенерировать текст области ОпределяемыеТипы
Функция СгенерироватьТекстОбласти_ОпределяемыеТипы() Экспорт
	
	ГруппаОбъектовМетаданных = Метаданные.ОпределяемыеТипы;
	
	НепроверенныеТипы = НепроверенныеТипы(ГруппаОбъектовМетаданных);
	
	мСтр = Новый Массив;
	
	мСтр.Добавить("#Область ПроверкаОпределяемыхТипов");
	мСтр.Добавить();
	
	мСтр.Добавить("Процедура ПроверитьОпределяемыеТипы(Отказ) Экспорт");
	мСтр.Добавить(Символы.Таб);
	мСтр.Добавить(Символы.Таб + "ПроверенныеТипы = Новый Соответствие;");
	мСтр.Добавить(Символы.Таб);
	
	Для Каждого КлючИЗначение Из НепроверенныеТипы Цикл
		ОбъектМетаданных = КлючИЗначение.Ключ;
		мСтр.Добавить(Символы.Таб + "ПроверитьОпределяемыйТип_" + ОбъектМетаданных.Имя + "(Отказ, ПроверенныеТипы);");
	КонецЦикла;
	
	мСтр.Добавить(Символы.Таб);
	мСтр.Добавить(Символы.Таб + "ЗаголовокСообщенияОбОшибке = ""Не выполняется проверка определяемых типов:"";");
	мСтр.Добавить(Символы.Таб + "мт_ТестированиеКонфигурации_Служебный.СообщитьОНепроверенныхТипах(ПроверенныеТипы, Отказ, Метаданные.ОпределяемыеТипы, ЗаголовокСообщенияОбОшибке);");
	мСтр.Добавить(Символы.Таб);
	мСтр.Добавить("КонецПроцедуры");
	мСтр.Добавить();
	
	Для Каждого КлючИЗначение Из НепроверенныеТипы Цикл
		
		ОбъектМетаданных = КлючИЗначение.Ключ;
		
		мСтр.Добавить("Процедура ПроверитьОпределяемыйТип_" + ОбъектМетаданных.Имя + "(Отказ, ПроверенныеТипы)");
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "МетаданныеОпределяемогоТипа = Метаданные.ОпределяемыеТипы." + ОбъектМетаданных.Имя + ";");
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "ТребуемыеТипы = Новый Массив;");
		
		мТребуемыеТипы = НепроверенныеТипы[ОбъектМетаданных];
		Если мТребуемыеТипы<>Неопределено Тогда
			Для Каждого Тип Из мТребуемыеТипы Цикл
				СтроковоеПредставлениеТипа = СтроковоеПредставлениеТипа(Тип);
				мСтр.Добавить(Символы.Таб + "ТребуемыеТипы.Добавить(Тип(""" + СтроковоеПредставлениеТипа + """));");
			КонецЦикла;
		КонецЕсли;
		
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить(Символы.Таб + "мт_ТестированиеКонфигурации_Служебный.ПроверитьВхождениеТипов_в_ТипМетаданного(МетаданныеОпределяемогоТипа, ТребуемыеТипы, Отказ, ПроверенныеТипы);");
		мСтр.Добавить(Символы.Таб);
		мСтр.Добавить("КонецПроцедуры");
		мСтр.Добавить();
		
	КонецЦикла;
	
	мСтр.Добавить("#КонецОбласти");
	мСтр.Добавить();
	
	ТекстОбласти = Новый ТекстовыйДокумент;
	Для Каждого Строчка Из мСтр Цикл
		ТекстОбласти.ДобавитьСтроку(Строчка);
	КонецЦикла;
	
	Возврат ТекстОбласти;
	
КонецФункции
	
#КонецОбласти

#Область Вспомогательные

Функция СтроковоеПредставлениеТипа(Тип)
	
	СтрокаXML = ЗначениеВСтрокуXML(Тип);
	
	//<Type xmlns="http://v8.1c.ru/8.1/data/core" xmlns:d1p1="http://v8.1c.ru/8.1/data/enterprise/current-config" 
	//xmlns:xs="http://www.w3.org/2001/XMLSchema" xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" 
	//xsi:type="Type">d1p1:DocumentRef.АвансовыйОтчет</Type>
	
	Тег = "xsi:type=""Type"">";
	ПозицияТега = Найти(СтрокаXML, Тег);
	Если ПозицияТега = 0 Тогда
		ВызватьИсключение "Ошибка 1 получения представления типа: " + СтрокаXML;
	КонецЕсли;
	
	Подстрока = Сред(СтрокаXML, ПозицияТега + СтрДлина(Тег));
	
	Тег = "</Type>";
	ПозицияТега = Найти(Подстрока, Тег);
	Если ПозицияТега = 0 Тогда
		ВызватьИсключение "Ошибка 2 получения представления типа: " + Подстрока;
	КонецЕсли;
	
	Подстрока = Лев(Подстрока, ПозицияТега - 1); 
	
	ПозицияДвоеточияПосле_d1p1 = СтрНайти(Подстрока, ":");
	Если ПозицияДвоеточияПосле_d1p1 = 0 Тогда
		ВызватьИсключение "Ошибка 3 получения представления типа: " + Подстрока;
	КонецЕсли;
	
	Подстрока = Сред(Подстрока, ПозицияДвоеточияПосле_d1p1 + 1);
	//здесь должно быть "DocumentRef.АвансовыйОтчет"
	
	ПозицияТочки = Найти(Подстрока, ".");
	Если ПозицияТочки = 0 Тогда
		ВызватьИсключение "Ошибка 4 получения представления типа: " + Подстрока;
	КонецЕсли;
	
	ЛеваяЧасть = Лев(Подстрока, ПозицияТочки - 1);
	ПраваяЧасть = Сред(Подстрока, ПозицияТочки + 1);
	
	Если ЛеваяЧасть = "DocumentRef" Тогда 
		ЛеваяЧасть = "ДокументСсылка"
	ИначеЕсли ЛеваяЧасть = "DocumentObject" Тогда 
		ЛеваяЧасть = "ДокументОбъект"
	ИначеЕсли ЛеваяЧасть = "CatalogRef" Тогда 
		ЛеваяЧасть = "СправочникСсылка"
	ИначеЕсли ЛеваяЧасть = "CatalogObject" Тогда 
		ЛеваяЧасть = "СправочникОбъект"
	ИначеЕсли ЛеваяЧасть = "BusinessProcessRef" Тогда 
		ЛеваяЧасть = "БизнесПроцессСсылка"
	ИначеЕсли ЛеваяЧасть = "BusinessProcessObject" Тогда 
		ЛеваяЧасть = "БизнесПроцессОбъект"
	Иначе 
		ВызватьИсключение "Непредусмотренный вариант: " + ЛеваяЧасть;
	КонецЕсли;
	
	Возврат ЛеваяЧасть + "." + ПраваяЧасть;
	
КонецФункции

Функция ЗначениеВСтрокуXML(Значение)
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, Значение, НазначениеТипаXML.Явное);
	
	Возврат ЗаписьXML.Закрыть();
КонецФункции

Функция ПреобразоватьПолноеИмяМетаданногоКоМножественномуЧислу(ПолноеИмя)
	
	Результат = ПолноеИмя;
	
	Если СтрНачинаетсяС(Результат, "Документ.") Тогда
		Результат =  "Документы." + Сред(Результат, СтрДлина("Документ.") + 1);
	ИначеЕсли СтрНачинаетсяС(Результат, "Справочник.") Тогда
		Результат = "Справочники." + Сред(Результат, СтрДлина("Справочник.") + 1);
	ИначеЕсли СтрНачинаетсяС(Результат, "БизнесПроцесс.") Тогда
		Результат = "БизнесПроцессы." + Сред(Результат, СтрДлина("БизнесПроцесс.") + 1);
	Иначе
		ВызватьИсключение "Непредусмотренное значение в ПреобразоватьПолноеИмяМетаданногоКоМножественномуЧислу: " + ПолноеИмя;
	КонецЕсли;
	
	Результат = СтрЗаменить(Результат, ".Реквизит.", ".Реквизиты.");
	Результат = СтрЗаменить(Результат, ".ТабличнаяЧасть.", ".ТабличныеЧасти.");
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоРегистрНакопления(МетаданныеРегистра)
	Возврат СтрНачинаетсяС(МетаданныеРегистра.ПолноеИмя(), "РегистрНакопления.");
КонецФункции

Функция ДополнитьСтрокуПробелами(Строка, ТребуемаяДлина, Выравнивание = "прижать влево")
	
	ТекущаяДлина = СтрДлина(Строка);
	Если ТекущаяДлина > ТребуемаяДлина Тогда
		возврат Строка;
	КонецЕсли;
	
	Пробелы = "";
	Для к = ТекущаяДлина+1 По ТребуемаяДлина Цикл
		Пробелы = Пробелы + " ";
	КонецЦикла;
	
	Если Выравнивание = "прижать влево" Тогда
		
		возврат Строка + Пробелы;
		
	ИначеЕсли Выравнивание = "прижать вправо" Тогда
		
		возврат Пробелы + Строка;
		
	ИначеЕсли Выравнивание = "по центру" Тогда
		
		ДлинаПробеловСлева = Окр(СтрДлина(Пробелы)/2, 0);
		ПробелыСлева = Лев(Пробелы, ДлинаПробеловСлева);
		ПробелыСправа = Сред(Пробелы, ДлинаПробеловСлева+1);
		возврат ПробелыСлева + Строка + ПробелыСправа;
		
	Иначе
		
		ВызватьИсключение "Некорректый параметр Выравнивание: "+Выравнивание;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ГенераторСценариевТестирования_VanessaAutomation

//Генерация текстов сценариев для тестирования средствами Vanessa Automation 
//Описание см. https://infostart.ru/public/1250707/

Функция ТекстыФичТестирования(СтрокаСоединения, ТестированиеВРабочейБазе) Экспорт
	
	Результат = Новый Соответствие;
	
	СценарииТестирования = СценарииТестирования();
	
	стрГруппировкаФич = "Раздел, Пользователь";
	тзГруппировкиФич = СценарииТестирования.Скопировать(, стрГруппировкаФич);
	тзГруппировкиФич.Свернуть(стрГруппировкаФич);
	
	Для Каждого ГруппировкаФичи Из тзГруппировкиФич Цикл
		
		ПользовательФичи = ГруппировкаФичи.Пользователь;
		
		НаименованиеГруппы = ?(ЗначениеЗаполнено(ГруппировкаФичи.Раздел), ГруппировкаФичи.Раздел + " ", "")
			+ ?(ЗначениеЗаполнено(ПользовательФичи), "под пользователем " + ПользовательФичи, "под полными правами");
		
		ОтборСценариев = Новый Структура(стрГруппировкаФич);
		ЗаполнитьЗначенияСвойств(ОтборСценариев, ГруппировкаФичи);
		СценарииФичи = СценарииТестирования.НайтиСтроки(ОтборСценариев);
		
		ТекстФайлаФичи = Новый	ТекстовыйДокумент;
		
		ТекстФайлаФичи.ДобавитьСтроку("#language: ru");
		ТекстФайлаФичи.ДобавитьСтроку("#encoding: utf-8");
		ТекстФайлаФичи.ДобавитьСтроку("#Сгенерирован обработкой СценарииТестированияОбъектовКонфигурации()");
		ТекстФайлаФичи.ДобавитьСтроку("");
		ТекстФайлаФичи.ДобавитьСтроку("Функционал: Проверка объектов конфигурации " + НаименованиеГруппы);
		ТекстФайлаФичи.ДобавитьСтроку("");
		ТекстФайлаФичи.ДобавитьСтроку("Как " + ?(ЗначениеЗаполнено(ПользовательФичи), ПользовательФичи, "Пользователь с полными правами"));
		ТекстФайлаФичи.ДобавитьСтроку("Я хочу проверить встроенные печатные формы, открытие/проведение документов/справочников");
		ТекстФайлаФичи.ДобавитьСтроку("Чтобы проверить свои доработки конфигурации");
		ТекстФайлаФичи.ДобавитьСтроку("");
		
		ИмяПодключения = ДобавитьТекстПодключенияКлиентаТестирования(ТекстФайлаФичи, ПользовательФичи, СтрокаСоединения);
		
		ТекстФайлаФичи.ДобавитьСтроку("");
	
		Для Каждого Сценарий Из СценарииФичи Цикл
			
			Если ТестированиеВРабочейБазе
				И Сценарий.ВыполнятьТолькоВТестовойБазе Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстФайлаФичи.ДобавитьСтроку("Сценарий: " + Сценарий.Наименование);
			ТекстФайлаФичи.ДобавитьСтроку("");
			Если ЗначениеЗаполнено(Сценарий.Комментарий) Тогда
				ТекстФайлаФичи.ДобавитьСтроку("	* " + Сценарий.Комментарий);
			КонецЕсли;
			
			Если Сценарий.Действие = Действие_ПроизвольныйСкрипт() Тогда
				
				ДобавитьТекстПроизвольногоСкрипта(ТекстФайлаФичи, Сценарий);
				
			ИначеЕсли Сценарий.Действие = Действие_ФормированиеПечатнойФормы() Тогда
				
				ДобавитьТекстСценарияТестированияПечатнойФормы(ТекстФайлаФичи, Сценарий);
			
			ИначеЕсли Сценарий.Действие = Действие_Открытие() Тогда
				
				Если Не ЗначениеЗаполнено(Сценарий.ИмяФормы) Тогда
					ВызватьИсключение "Не указано имя формы в сценарии " + Сценарий.Наименование;
				КонецЕсли;
			
				ТекстФайлаФичи.ДобавитьСтроку("	Когда я открываю навигационную ссылку """ + Сценарий.НавигационнаяСсылка + """");
				ТекстФайлаФичи.ДобавитьСтроку("	Тогда открылась форма с именем '" + Сценарий.ИмяФормы + "'");
				ТекстФайлаФичи.ДобавитьСтроку("	И Я закрываю текущее окно");
				ТекстФайлаФичи.ДобавитьСтроку("	");
				
			ИначеЕсли Сценарий.Действие = Действие_Запись() Тогда
				
				ВызватьИсключение "Проверка записи не реализована";
				
			Иначе
			
				ВызватьИсключение СтрШаблон("Непредусмотренное действие сценария тестирования %1: %2", Сценарий.Наименование, Сценарий.Действие);;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ДобавитьТекстОтключенияКлиентаТестирования(ТекстФайлаФичи, ИмяПодключения);
		
		Результат.Вставить(НаименованиеГруппы, ТекстФайлаФичи); 
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СценарииТестирования()
	
	СценарииТестирования = ПустаяТаблицаСценариевТестирования();
	
	// Сценарии тестирования объектов конфигурации
	Для Каждого Менеджер Из мт_ТестированиеКонфигурации_Настройки.МенеджерыОбъектовСоСценариямиТестирования() Цикл
		Менеджер.ДобавитьСценарииТестирования(СценарииТестирования);
	КонецЦикла;
	
	// Сценарии тестирования внешних печатных форм
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДополнительныеОтчетыИОбработки.Ссылка КАК Ссылка,
	               |	ДополнительныеОтчетыИОбработки.Наименование КАК Наименование
	               |ИЗ
	               |	Справочник.ДополнительныеОтчетыИОбработки КАК ДополнительныеОтчетыИОбработки
	               |ГДЕ
	               |	ДополнительныеОтчетыИОбработки.Публикация <> ЗНАЧЕНИЕ(Перечисление.ВариантыПубликацииДополнительныхОтчетовИОбработок.Отключена)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ДополнительныеОтчетыИОбработки.Наименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ХранилищеОбработки = Выборка.Ссылка.ХранилищеОбработки;
		ДвоичныеДанные = ХранилищеОбработки.Получить();
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("epf");
		ДвоичныеДанные.Записать(ИмяВременногоФайла);
		ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяВременногоФайла);
		
		Попытка
			ВнешняяОбработка.ДобавитьСценарииТестирования(СценарииТестирования);
		Исключение
			ТекстОшибки = "Внешняя печатная форма " + Выборка.Наименование + ": " 
				+ КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат СценарииТестирования;
	
КонецФункции

Процедура ДобавитьТекстНачалаСценария(ТекстФайлаФичи, Сценарий)
	
	ТекстФайлаФичи.ДобавитьСтроку("Сценарий: " + Сценарий.Наименование);
	ТекстФайлаФичи.ДобавитьСтроку("");
	Если ЗначениеЗаполнено(Сценарий.Комментарий) Тогда
		ТекстФайлаФичи.ДобавитьСтроку("	* " + Сценарий.Комментарий);
	КонецЕсли;
			
КонецПроцедуры

Процедура ДобавитьТекстПроизвольногоСкрипта(ТекстФайлаФичи, Сценарий)
		
	Для Каждого СтрокаПроизвольногоСкрипта Из Сценарий.СтрокиПроизвольногоСкрипта Цикл
		
		// команда "я выбираю точное значение" предполагает выбор из формы списка быстрого выбора.
		// Требуемого элемента в этом списке может не быть
		Если СтрНайти(СтрокаПроизвольногоСкрипта, "я выбираю точное значение") <> 0 Тогда
			ВызватьИсключение "Команду сценария ""я выбираю точное значение"" следует заменить на ""я выбираю по строке""  
				|" + Сценарий.Наименование;
		КонецЕсли;
		
		Если НЕ Сценарий.ВыполнятьТолькоВТестовойБазе Тогда
			Если СтрНайти(СтрокаПроизвольногоСкрипта, "Провести") <> 0
				Или СтрНайти(СтрокаПроизвольногоСкрипта, "Записать") <> 0 Тогда
				ВызватьИсключение "В сценарии используется команда записи. 
					|Установите для этого сценария признак Сценарий.ВыполнятьТолькоВТестовойБазе = Истина
					|" + Сценарий.Наименование;
			КонецЕсли;
		КонецЕсли;
			
		ТекстФайлаФичи.ДобавитьСтроку("	" + СтрокаПроизвольногоСкрипта);
	КонецЦикла;
	ТекстФайлаФичи.ДобавитьСтроку("	");
		
КонецПроцедуры

Процедура ДобавитьТекстСценарияТестированияПечатнойФормы(ТекстФайлаФичи, Сценарий)
	
	ТекстФайлаФичи.ДобавитьСтроку("	Когда я открываю навигационную ссылку """ + Сценарий.НавигационнаяСсылка + """");
	
	КомандаПоИмени = Истина;
	КомандаПоЗаголовкуКнопки = Ложь;
	
	Для Каждого Команда Из Сценарий.Команды Цикл
		Добавить_Один_ТекстСценарияТестированияПечатнойФормы(ТекстФайлаФичи, Сценарий, Команда, КомандаПоИмени);
	КонецЦикла;
	
	Для Каждого Команда Из Сценарий.Команды_ПоЗаголовку Цикл
		Добавить_Один_ТекстСценарияТестированияПечатнойФормы(ТекстФайлаФичи, Сценарий, Команда, КомандаПоЗаголовкуКнопки);
	КонецЦикла;
	
	ТекстФайлаФичи.ДобавитьСтроку("	И Я закрываю текущее окно"); //окно объекта навигационной ссылки
	ТекстФайлаФичи.ДобавитьСтроку("	");
		
КонецПроцедуры

Процедура Добавить_Один_ТекстСценарияТестированияПечатнойФормы(ТекстФайлаФичи, Сценарий, Команда, КомандаПоИмени=Истина)
	
	Если КомандаПоИмени Тогда
		ТекстФайлаФичи.ДобавитьСтроку("	И я нажимаю на кнопку с именем 'ПодменюПечатьОбычное_" + Команда + "'");
	Иначе //по заголовку кнопки
		ТекстФайлаФичи.ДобавитьСтроку("	И я нажимаю на кнопку '" + Команда + "'");
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Сценарий.ИмяФормыОтчета) Тогда
		ТекстФайлаФичи.ДобавитьСтроку("	Тогда открылась форма с именем '" + Сценарий.ИмяФормыОтчета + "'");
	Иначе
		ТекстФайлаФичи.ДобавитьСтроку("	Тогда открылась форма с именем 'ОбщаяФорма.ПечатьДокументов'");
	КонецЕсли;
	
	ИмяТабличногоДокумента = Сценарий.ИмяТабличногоДокумента;
	Если Не ЗначениеЗаполнено(ИмяТабличногоДокумента) Тогда
		ИмяТабличногоДокумента = "ТекущаяПечатнаяФорма";
	КонецЕсли;
	
	Если Сценарий.СтрокиПроизвольногоСкрипта.Количество() = 0 Тогда
		ТекстФайлаФичи.ДобавитьСтроку("	И в табличном документе '" + ИмяТабличногоДокумента + "' я перехожу к ячейке ""R1C1""");
	Иначе
		ДобавитьТекстПроизвольногоСкрипта(ТекстФайлаФичи, Сценарий);
	КонецЕсли;
	
	ТекстФайлаФичи.ДобавитьСтроку("	И Я закрываю текущее окно"); //окно печатной формы
	ТекстФайлаФичи.ДобавитьСтроку("	");
		
КонецПроцедуры

Функция ДобавитьТекстПодключенияКлиентаТестирования(ТекстФайлаФичи, Пользователь, СтрокаСоединения)
	
	Если ЗначениеЗаполнено(Пользователь) Тогда
		ИмяПодключения = "ТестоваяБаза_" + Пользователь;
		ИмяИПарольПользователя = мт_ТестированиеКонфигурации_Настройки.ИмяИПарольПользователя(Пользователь);
		ИмяПользователя = ИмяИПарольПользователя.Имя;
		ПарольПользователя = ИмяИПарольПользователя.Пароль;
	Иначе
		ИмяПодключения = "ТестоваяБаза_ПолныеПрава";
		ИмяПользователя = "";
		ПарольПользователя = "";
	КонецЕсли;
	
	//для красоты
	ШиринаКолонки_ИмяПодключения = 30;
	ШиринаКолонки_СтрокаСоединения = 50;
	ШиринаКолонки_Логин = 20;
	ШиринаКолонки_Пароль = 15;
		
	ТекстФайлаФичи.ДобавитьСтроку("Сценарий: Подключение клиента тестирования '"+ИмяПодключения+"'");
	ТекстФайлаФичи.ДобавитьСтроку("	Когда Я подключаю клиент тестирования с параметрами:");
	ТекстФайлаФичи.ДобавитьСтроку(
		СтрШаблон("	| %1| 'Синоним'     | 'Порт' | %2| %3| %4| 'Запускаемая обработка' |  'Дополнительные параметры строки запуска'  |"
			, ДополнитьСтрокуПробелами("'Имя подключения'", ШиринаКолонки_ИмяПодключения)
			, ДополнитьСтрокуПробелами("'Строка соединения'", ШиринаКолонки_СтрокаСоединения)
			, ДополнитьСтрокуПробелами("'Логин'", ШиринаКолонки_Логин)
			, ДополнитьСтрокуПробелами("'Пароль'", ШиринаКолонки_Пароль)
		));
	ТекстФайлаФичи.ДобавитьСтроку(
		СтрШаблон("	| %1| ''            | '0'    | %2| %3| %4| ''                      |  ''                                         |"
			, ДополнитьСтрокуПробелами("'"+ИмяПодключения+"'", ШиринаКолонки_ИмяПодключения)
			, ДополнитьСтрокуПробелами("'"+СтрокаСоединения+"'", ШиринаКолонки_СтрокаСоединения)
			, ДополнитьСтрокуПробелами("'"+ИмяПользователя+"'", ШиринаКолонки_Логин)
			, ДополнитьСтрокуПробелами("'"+ПарольПользователя+"'", ШиринаКолонки_Пароль)
		));
		
	ТекстФайлаФичи.ДобавитьСтроку("");
	
	Возврат ИмяПодключения;

КонецФункции

Процедура ДобавитьТекстОтключенияКлиентаТестирования(ТекстФайлаФичи, ИмяПодключения)
	
	ТекстФайлаФичи.ДобавитьСтроку("Сценарий: Отключение клиента тестирования '"+ИмяПодключения+"'");
	ТекстФайлаФичи.ДобавитьСтроку("	И я закрываю TestClient """+ИмяПодключения+"""");
	ТекстФайлаФичи.ДобавитьСтроку("");
	
КонецПроцедуры

Функция ПустаяТаблицаСценариевТестирования()
	
	ТаблицаСценариев = Новый ТаблицаЗначений;
	
	// разделы предназначены для более детальной группировки фич. 
	// Например, для нового сценария можно указать раздел "Отладка", и проверять работу только этой фичи с одним сценарием тестирования.
	// Значение по умолчанию - пустая строка
	ТаблицаСценариев.Колонки.Добавить("Раздел", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));
	
	// Фичи группируются по пользователям и разделам
	ТаблицаСценариев.Колонки.Добавить("Пользователь", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));
	
	// Наименование сценария
	ТаблицаСценариев.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	ТаблицаСценариев.Колонки.Добавить("Комментарий", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	
	// При тестировании печатных форм можно указать навигационную ссылку документа и набор команд печати
	ТаблицаСценариев.Колонки.Добавить("НавигационнаяСсылка", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	
	// Значения: Действие_ФормированиеПечатнойФормы(), Действие_Открытие(), Действие_Запись(), Действие_ПроизвольныйСкрипт()
	ТаблицаСценариев.Колонки.Добавить("Действие", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(1,0,ДопустимыйЗнак.Неотрицательный)));
	
	// Для сценариев, изменяющих данные в базе, необходимо указать в этом поле Истина.
	// Этот сценарий не будет выполняться при тестировании в рабочей базе.
	ТаблицаСценариев.Колонки.Добавить("ВыполнятьТолькоВТестовойБазе", Новый ОписаниеТипов("Булево"));
	
	//при проверке просмотра/записи справочников, документов
	ТаблицаСценариев.Колонки.Добавить("ИмяФормы", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	ТаблицаСценариев.Колонки.Добавить("ДополнительныеСтрокиСкрипта", Новый ОписаниеТипов("Массив"));
	
	// При проверке печатных форм - имя открывающейся при печати формы и имя табличного документа в этой форме
	ТаблицаСценариев.Колонки.Добавить("ИмяФормыОтчета", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100))); //по умолчанию "ОбщаяФорма.ПечатьДокументов"
	ТаблицаСценариев.Колонки.Добавить("ИмяТабличногоДокумента", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50))); 	//по умолчанию: "ТекущаяПечатнаяФорма"
																																//для отчетов на СКД: "ОтчетТабличныйДокумент"
																															
	// При проверке печатных форм - имена кнопок или заголовки кнопок
	ТаблицаСценариев.Колонки.Добавить("Команды", Новый ОписаниеТипов("Массив")); 
	ТаблицаСценариев.Колонки.Добавить("Команды_ПоЗаголовку", Новый ОписаниеТипов("Массив"));
	
	// Произвольный скрипт. При тестировании печатных форм выполняется после формирования печатной формы
	ТаблицаСценариев.Колонки.Добавить("СтрокиПроизвольногоСкрипта", Новый ОписаниеТипов("Массив"));
	
	возврат ТаблицаСценариев;
	
КонецФункции

Функция Действие_ФормированиеПечатнойФормы() Экспорт
	возврат 0;
КонецФункции

Функция Действие_Открытие() Экспорт
	возврат 1;
КонецФункции

Функция Действие_Запись() Экспорт
	возврат 2;
КонецФункции

Функция Действие_ПроизвольныйСкрипт() Экспорт
	возврат 3;
КонецФункции

Функция ПроверитьНаличиеСценариевТестирования_ПечатныеФормы(ВнешнийОбъект) Экспорт
	
	ТекстСообщенияОбОшибке = "Не указаны сценарии тестирования! См. описание в общем модуле ""мт_ТестированиеКонфигурации_Служебный""";
	
	СценарииТестирования = ПустаяТаблицаСценариевТестирования();
	Попытка
		ВнешнийОбъект.ДобавитьСценарииТестирования(СценарииТестирования);
		Если СценарииТестирования.Количество() = 0 Тогда
			ВызватьИсключение ТекстСообщенияОбОшибке;
		КонецЕсли;
	Исключение
		ВызватьИсключение ТекстСообщенияОбОшибке;
	КонецПопытки;

КонецФункции

Процедура ДобавитьТекстВСценарий(Сценарий, Текст) Экспорт
	
	Для н=1 По СтрЧислоСтрок(Текст) Цикл
		Сценарий.СтрокиПроизвольногоСкрипта.Добавить(СтрПолучитьСтроку(Текст, н));
	КонецЦикла;

КонецПроцедуры

//пример процедуры ДобавитьСценарииТестирования в модуле внешнего отчета/обработки:

//ПРИМЕР №1. Простая проверка

//Процедура ДобавитьСценарииТестирования(СценарииТестирования) Экспорт
//	
//	Сценарий = СценарииТестирования.Добавить();
//	Сценарий.Наименование = "Формирование печатной формы ""Акт о списании товаров (ТОРГ-16)""";
//	Сценарий.Комментарий = "Списание из эксплуатации № 00УП-000005 от 31.03.2020";
//	Сценарий.НавигационнаяСсылка = "e1cib/data/Документ.СписаниеИзЭксплуатации?ref=918700155d90cf0011ea88979ad3f7e3";
//	
//	мКоманды = Новый Массив;
//	Для Каждого Команда Из СведенияОВнешнейОбработке().Команды Цикл
//		мКоманды.Добавить(Команда.Идентификатор);
//	КонецЦикла;
//	
//	Сценарий.Команды = мКоманды;
//	
//КонецПроцедуры

//ПРИМЕР № 2. Произвольный скрипт

//Процедура ДобавитьСценарииТестирования(СценарииТестирования) Экспорт
//	
//	Сценарий = СценарииТестирования.Добавить();
//	Сценарий.Наименование = "Формирование печатной формы ""Паспорт готового изделия""";
//	Сценарий.Комментарий = "Проверка ОКК № 000003799 от 22.10.2018";
//	
//	Сценарий.ПроизвольныйСкрипт = Истина;
//	
//	|	Когда я открываю навигационную ссылку ""e1cib/data/Документ.мт_ПроверкаОТК?ref=80deac220b889cff11e8d5f8b14b622f""");
//	|	И я нажимаю на кнопку с именем 'ПодменюПечатьОбычное_ПаспортГотовогоИзделия'");
//	|	Тогда открылось окно 'Паспорт готового изделия'");
//	|	И в поле 'Поместить в папку' я ввожу текст 'C:\Users\bexpert\Тест'");
//	|	И я нажимаю на кнопку 'Сформировать без сохранения'");
//	|	Тогда открылось окно 'Показать папку с паспортами'");
//	|	И я нажимаю на кнопку 'Нет'");
//	
//КонецПроцедуры

//ПРИМЕР №3. В этой внешней печатной форме проверка не нужна

//Процедура ДобавитьСценарииТестирования(СценарииТестирования) Экспорт
//	
//	Сценарий = СценарииТестирования.Добавить();
//	Сценарий.Наименование = "Обработка ""Заполнить регистр (мт) Движение производственных партий""";
//	Сценарий.Комментарий = "Проверка не требуется";
//	
//	Сценарий.ПроизвольныйСкрипт = Истина;
//	
//КонецПроцедуры

//ПРИМЕР №4. Открытие документа пользователем с ограниченными правами

//Процедура ДобавитьСценарииТестирования(СценарииТестирования) Экспорт
//	
//	Сценарий = СценарииТестирования.Добавить();
//	
//	Сценарий.Пользователь = мт_ТестированиеКонфигурации_Настройки.Пользователь_МастерСборки();
//	Сценарий.Действие = мт_ТестированиеКонфигурации_Служебный.Действие_Открытие();
//	
//	Сценарий.Наименование = "Возможность просмотра документа мастером сборки";
//	Сценарий.Комментарий = "Проверка прав просмотра ОКК № 000007834 от 09.06.2020";
//	Сценарий.НавигационнаяСсылка = "e1cib/data/Документ.мт_ПроверкаОТК?ref=918800155d90cf0011eaaa4b43515fb3";
//	Сценарий.Комментарий = "Проверка открытия ОКК № 000007834 от 09.06.2020";
//	Сценарий.ИмяФормы = "Документ.мт_ПроверкаОТК.Форма.ФормаДокумента";
//	
//КонецПроцедуры

//ПРИМЕР №5. Формирование встроенной печатной формы
 
//Процедура ДобавитьСценарииТестирования(СценарииТестирования) Экспорт
//	
//	Сценарий = СценарииТестирования.Добавить();
//	Сценарий.Действие = мт_ТестированиеКонфигурации_Служебный.Действие_ФормированиеПечатнойФормы();
//	Сценарий.Наименование = "Формирование печатных форм производственной партии";
//	Сценарий.Комментарий = "Производственная партия П555Мр20-3";
//	Сценарий.НавигационнаяСсылка = "e1cib/data/Документ.мт_ПроизводственныеПартии?ref=918700155d90cf0011ea6790593e9fbd";
//	
//	мКоманды = Новый Массив;
//	мКоманды.Добавить("МаршрутнаяКарта");
//	мКоманды.Добавить("СерийныеНомераПартии");
//	Сценарий.Команды = мКоманды;
//	
//	////
//	
//	Сценарий = СценарииТестирования.Добавить();
//	Сценарий.Действие = мт_ТестированиеКонфигурации_Служебный.Действие_ФормированиеПечатнойФормы();
//	Сценарий.Наименование = "Формирование печатных форм производственной партии (2)";
//	Сценарий.Комментарий = "Производственная партия П555Мр20-3";
//	Сценарий.НавигационнаяСсылка = "e1cib/data/Документ.мт_ПроизводственныеПартии?ref=918700155d90cf0011ea6790593e9fbd";
//	
//	мКоманды = Новый Массив;
//	мКоманды.Добавить("ВыполнениеПроизводственныхОпераций");
//	
//	Сценарий.ИмяФормыОтчета = "Отчет.мт_ВыполнениеПроизводственныхОпераций.ФормаОбъекта";
//	Сценарий.Команды = мКоманды;
//	
//	////
//	
//	Сценарий = СценарииТестирования.Добавить();
//	Сценарий.Действие = мт_ТестированиеКонфигурации_Служебный.Действие_ФормированиеПечатнойФормы();
//	Сценарий.Наименование = "Формирование печатных форм производственной партии (3)";
//	Сценарий.Комментарий = "Производственная партия П555Мр20-3";
//	Сценарий.НавигационнаяСсылка = "e1cib/data/Документ.мт_ПроизводственныеПартии?ref=918700155d90cf0011ea6790593e9fbd";
//	
//	мКоманды = Новый Массив;
//	мКоманды.Добавить("РезультатыПроверкиОТК_ПоПартии");
//	
//	Сценарий.ИмяФормыОтчета = "Отчет.мт_РезультатыПроверкиОТК_ПоПартии.ФормаОбъекта";
//	Сценарий.Команды = мКоманды;
//	
//КонецПроцедуры

#КонецОбласти

